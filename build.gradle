plugins {
    id 'java'
    id 'maven'
    id 'net.minecrell.licenser' version '0.2.1'
    id 'ninja.miserable.blossom' version '1.0.1'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        name 'Sponge maven repo'
        url 'http://repo.spongepowered.org/maven/'
    }
}

ext.api = project(':XtraAPI')
project.version = api.version

dependencies {
    compile api
    compile 'org.reflections:reflections:0.9.10'
    compile 'org.javassist:javassist:3.20.0-GA'
    compile 'org.apache.logging.log4j:log4j-core:2.0-beta9'
}

blossom {
    replaceToken '@project.version@', project.version, 'src/main/java/com/xtra/core/internal/Internals.java'
}

license {
    ext.name = project.name
    ext.organization = project.organization
    ext.url = project.url
    ext.inceptionYear = project.inceptionYear
    ext.currentYear = project.currentYear
    header rootProject.file('HEADER.txt')
    include '**/*.java'
    style.java = 'JAVADOC'
}

shadowJar {
    dependencies {
        include(dependency('org.reflections:reflections'))
        include(dependency('org.javassist:javassist'))
        include(dependency(':XtraAPI'))
    }
    relocate 'org.reflections', 'com.xtra.core.core-dependencies.reflections'
    relocate 'javassist', 'com.xtra.core.core-dependencies.javassist'
    relocate 'com.xtra.api', 'com.xtra.core.core-dependencies.api'
    classifier = ''
}

artifacts {
    archives shadowJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}
